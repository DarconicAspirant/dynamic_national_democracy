title: The Economic Crisis
new-page: true
is-card: true
view-if: (crisis_urgency >= 2 or year >= 1931 or (month >= 6 and year == 1930)) and crisis_program_timer = 0 and economic_plan = 0 and black_thursday_seen == 1
frequency: 200
on-arrival: month_actions += 1; crisis_program_timer = 5
card-image: img/bankrun.jpg
tags: party_affairs, govt_affairs

= Addressing the Economic Crisis

Our party is wracked as to how to deal with the economic crisis. From the centrist faction, Hilferding argues that the economic crisis must be allowed to play out, because it is simply a part of the cyclic nature of capitalism.

[? if not wtb_concept: The Labor faction is arguing for increased welfare spending to alleviate the suffering of the workers. Their economist, Wladimir Woytinsky, has voiced support for a public works campaign involving deficit spending, but it will take time to iron out the details. Furthermore, he will need the united support of the ADGB; otherwise, he won't have the power to challenge Hilferding's stance. ?][? if wtb_concept: From labor, the economist Wladimir Woytinsky argues that we must pursue a massive public works program to reduce unemployment and stimulate demand, perhaps including deficit spending. His opponents, like Hilferding, argue that this plan will cause a return to hyper-inflation, and that it would not even help the economy. In addition, according to Hilferding, Woytinsky's strategy is opposed to Marxist principles. ?]

[? if wtb_concept: The so-called WTB plan, after its chief supporters Woytinsky, Tarnow, and Baade, is receiving more attention within the party. ?]

However, leftists in the party argue that the WTB plan is merely a bandage over the fundamental flaws of capitalism. They argue that the crisis is an opportunity to end capitalism through massive nationalizations of industry, and finally bring our country to <span style="color: #c00000;">socialism</span>.

Meanwhile, moderates and reformists draw up proposals for a limited plan of job creation through public works, funded through taxes, that does not involve deficit spending or large-scale restructuring of the economy. Labor argues that the plan is inadequate in its scale and ambition.

- @support_centrist: Support the Centrist view.
- @support_labor: Support the Labor view.
- @support_labor_pre: Support the Labor view.
- @support_left: Support the Left view.
- @support_moderate: Support the Reformist view.
- @defer: Defer making a decision until we have more information.

@support_centrist
subtitle: Let the depression play itself out.
on-arrival: agrarian_strength += 5; center_dissent -= 5; christsoc_dissent += 10; worksplan_support -= 1; right_dissent += 10;

Woytinsky and the trade unions vehemently criticize the centrists for their policy. The workers are starving; are we going to callously abandon them and side with bourgeois austerity? On the other hand, the centrists criticize Woytinsky for promising the workers false hope; their position is that nothing can be done to alleviate the depression. They also speak of the fear of another round of hyperinflation that may result from deficit spending.

Our policy course is currently that nothing can be done to alleviate the depression.

@support_labor
subtitle: Call for increased public works and fiscal stimulus.
unavailable-subtitle: The Labor faction is not strong enough.
choose-if: christsoc_strength >= right_strength and christsoc_strength >= agrarian_strength
view-if: wtb_concept
on-arrival: center_dissent += 10; right_dissent += 5; christsoc_strength += 5; worksplan_support += 1; wtb_points += 10

Our support of the WTB plan alienates the orthodox Marxists of the Centrist faction, and faces both opposition and support from a variety of sources across the spectrum. Leftists argue that the plan is flawed because it leaves capitalism intact, while some reformists are cautiously optimistic.

We have not yet convinced the party to formally adopt the plan. More work needs to be done.

@support_labor_pre
subtitle: Call for welfare spending and emergency relief for the workers.
view-if: not wtb_concept
on-arrival: center_dissent += 5; right_dissent += 5; christsoc_strength += 5; christsoc_dissent -= 5; worksplan_support += 1

The Labor faction hasn't come up with a concrete solution to our economic woes yet, but has called for increased welfare spending in light of the rise in unemployment. Perhaps supporting their efforts will encourage a real solution to the crisis to be introduced.

@support_left
subtitle: Call for mass nationalizations and the transformation of the economy.
unavailable-subtitle: The Left faction is not strong enough.
choose-if: right_strength >= christsoc_strength and right_strength >= agrarian_strength and right_strength >= popcon_strength
on-arrival: right_strength += 10; popcon_dissent += 5; christsoc_dissent += 5; neoliberal_plan_support += 2
go-to: support_left_progress if neoliberal_plan_support < 5; adopt_left if neoliberal_plan_support >= 5

@support_left_progress

We attempt to convince the party to support a program of mass nationalizations in response to the economic crisis. Leftists argue that this policy is in accordance with our long-standing <span style="color: #c00000;">socialist</span> platform, while reformists and some trade unionists argue that the plan is unrealistic, and that we need to focus on more immediate improvements to workers' lives.

We have not yet formally adopted the plan. More work needs to be done.

@adopt_left
on-arrival: right_strength += 15; right_dissent -= 10; popcon_dissent += 10; paramilitarist_dissent += 8; christsoc_dissent += 6; neoliberal_plan_adopted = 1; coup_progress += 1; economic_plan = 3; nsdap_relation += 5*(1-dissent)

With our hard work, we have convinced the majority of the party to adopt the <span style="color: #c00000;">socialist</span> economic plan! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?] Bourgeois opposition is likely to be fierce. [? if spd_in_government == 0 : It might be difficult to enact the plan, given present conditions. ?]

@support_moderate
subtitle: Call for a limited program of job creation without deficit spending.
unavailable-subtitle: The Reformist faction is not strong enough.
choose-if: popcon_strength >= right_strength and popcon_strength >= agrarian_strength
on-arrival: moderate_plan_support += 1; popcon_strength += 8; popcon_dissent -= 5; right_dissent += 6; christsoc_dissent += 5
go-to: adopt_moderate if moderate_plan_support >= 3; support_moderate_progress if moderate_plan_support < 3

@support_moderate_progress

We attempt to convince the party to support a moderate program of job creation without deficit spending. Advocates of the moderate plan argue that this plan is most likely to receive support from the bourgeois parties, and most practical in the immediate term. Opponents in the left and labor factions criticize the plan for being inadequate in its ambition.

We have not yet adopted the plan. More work needs to be done.

@adopt_moderate
on-arrival: reformst_strength += 10; popcon_dissent -= 10; right_dissent += 8; christsoc_dissent += 6; moderate_plan_adopted = 1; economic_plan = 2; coalition_dissent -= 1 if unemployed > 15; z_relation += 5;

With our hard work, we have convinced the majority of the party to adopt the moderate job creation plan! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?]

@defer

We hold off on making a decision on economic policy, awaiting more information. Woytinsky and his supporters argue that inaction is a form of action of its own. 
